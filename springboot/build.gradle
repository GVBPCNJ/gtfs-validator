/*
 * Copyright 2021 MobilityData IO
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'application'
    id 'java'
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

applicationDefaultJvmArgs = [ "-Xmx8G" ]

group 'org.mobilitydata'
version "${System.getenv("versionTag")}"

group 'org.mobilitydata'
version "${System.getenv("versionTag")}_springboot"

jar {
    // Add the manifest within the JAR, using gtfs-validator as the title
    manifest {
        attributes('Implementation-Title': rootProject.name,
                'Implementation-Version': project.version,
                'Main-Class': 'org.mobilitydata.gtfsvalidator.springboot.Main',
                'Multi-Release': 'true')
    }
}

shadowJar {
    // Change the JAR name from springboot to gtfs-validator
    archiveBaseName = rootProject.name
    // Remove "-all" from the end of the JAR file name
    archiveClassifier = null
    // Add "v" to version to match previous releases
    archiveVersion = project.version
}

sourceCompatibility = JavaVersion.VERSION_1_8
mainClassName = 'org.mobilitydata.gtfsvalidator.springboot.Main'

repositories {
    mavenCentral()
}

dependencies {
    // to be able to run org.mobilitydata.gtfsvalidator.main.Main
    implementation project(':core')
    implementation project(':main')

    // to use Google Cloud Storage services
    implementation 'com.google.cloud:google-cloud-storage:2.1.0'
    implementation platform('com.google.cloud:libraries-bom:21.0.0')

    // to convert query parameters as CLI arguments to be used in org.mobilitydata.gtfsvalidator.main.Main
    implementation 'com.beust:jcommander:1.81'

    // to log
    implementation 'com.google.flogger:flogger:0.6'
    implementation 'com.google.flogger:flogger-system-backend:0.6'

    // to use the CLI application as a web application via Spring Boot framework
    implementation 'org.springframework.boot:spring-boot-starter:2.5.4'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.5.4'

    // to implement websocket
    implementation 'org.springframework:spring-websocket:5.3.9'
    implementation 'org.springframework:spring-messaging:5.3.9'

    // to test GtfsValidatorController
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.4'
    testImplementation 'org.mockito:mockito-core:3.3.0'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
}

test {
    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Share the test report data to be aggregated for the whole project
configurations {
    binaryTestResultsElements {
        canBeResolved = false
        canBeConsumed = true
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, 'test-report-data'))
        }
        outgoing.artifact(test.binaryResultsDirectory)
    }
}
